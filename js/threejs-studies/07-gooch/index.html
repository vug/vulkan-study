<!DOCTYPE html>
<html>

<head>
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
  <title>Depth Visualization Study</title>
  <style>
    body {
      margin: 0;
    }

    #container {
      position: absolute;
      top: 0px;
      width: 100%;
      bottom: 0px;
    }
  </style>
</head>

<body>
  <div id="container"></div>
  <script id="post-vert" type="x-shader/x-vertex">
    struct VertexData {
      vec3 position;
      vec3 normal;
      vec2 uv;
    };
    out VertexData v2f;

    void main() {
      v2f.position = position;
      v2f.normal = normal;
      v2f.uv = uv;
      gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
    }
  </script>

  <script id="post-vert-raw" type="x-shader/x-vertex">
    in vec3 position;
    in vec3 normal;
    in vec2 uv;
    
    uniform mat4 modelViewMatrix;
    uniform mat4 projectionMatrix;    
        
    struct VertexData {
      vec3 position;
      vec3 normal;
      vec2 uv;
    };
    out VertexData v2f;

    void main() {
      v2f.position = position;
      v2f.normal = normal;
      v2f.uv = uv;
      gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
    }
  </script>

  <script id="post-outline-raw" type="x-shader/x-fragment">
    precision highp float;
    precision highp int;      

    struct VertexData {
      vec3 position;
      vec3 normal;
      vec2 uv;
    };
    in VertexData v2f;
    
    uniform float time;
    uniform sampler2D tDiffuse;
    uniform sampler2D tDepth;
    struct Camera {
      float near;
      float far;
    };
    uniform Camera cam;
    uniform int linearizationMethod;
    uniform float param;

    layout(location = 0) out vec4 fragOutline;
    layout(location = 1) out vec4 fragDepth;

    float linearDepthFromBufferUgur(float depth, float near, float far) {
      float zView = (near * far) / ((near - far) * depth + far);
      return zView;
    }

    // from https://learnopengl.com/Advanced-OpenGL/Depth-testing
    float linearDepthFromBuffer(float depth, float near, float far) {
      float zNDC = depth * 2.0 - 1.0; // back to NDC 
      float zView = (2.0 * near * far) / (far + near - zNDC * (far - near));	
      return zView;
    }

    float normalizeDepth(float zView, float near, float far) {
      return (zView - near) / (far - near);      
    }

    void main()	{
      vec3 originalColor = texture( tDiffuse, v2f.uv ).rgb;
      float depth = texture( tDepth, v2f.uv ).r; // from Depth Buffer, which is modified / non-linear
      float linearDepth = linearDepthFromBufferUgur(depth, cam.near, cam.far);
      
      fragDepth.rgb = vec3(normalizeDepth(linearDepth, cam.near, cam.far));
      fragDepth.a = 1.0;
      float diff = fwidth(linearDepth);
      //fragOutline.rgb = vec3(abs(diff));
      //fragOutline.rgb = vec3(step(diff, param));
      fragOutline.rgb = vec3(1.0 - smoothstep(param, param + 0.005, diff));
      fragOutline.a = 1.0;      
    }
  </script>

  <script id="post-screen" type="x-shader/x-fragment">
    struct VertexData {
      vec3 position;
      vec3 normal;
      vec2 uv;
    };
    in VertexData v2f;
    
    uniform sampler2D tTexture;

    void main()	{
      gl_FragColor.rgb = texture2D( tTexture, v2f.uv ).rgb;
      gl_FragColor.a = 1.0;      
    }    
  </script>

  <script id="post-compose" type="x-shader/x-fragment">
    struct VertexData {
      vec3 position;
      vec3 normal;
      vec2 uv;
    };
    in VertexData v2f;
    
    uniform sampler2D tScene;
    uniform sampler2D tOutline;

    void main()	{
      vec3 sceneColor = texture2D( tScene, v2f.uv ).rgb;
      vec3 outline = texture2D( tOutline, v2f.uv ).rgb;
      gl_FragColor.rgb = sceneColor * outline;
      gl_FragColor.a = 1.0;      
    }    
  </script>

  <script type="importmap">
    {
        "imports": {
            "three": "../node_modules/three/build/three.module.js",
            "three/addons/": "../node_modules/three/examples/jsm/"
        }
    }
  </script>
  <script type="module" src="main.js"></script>
</body>

</html>